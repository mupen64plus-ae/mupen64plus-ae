apply plugin: 'com.android.application'
apply plugin: 'signing'
apply from: '../build_common/version_common.gradle'

android {

    allprojects {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }

        repositories {
            mavenCentral()
            google()
        }
    }

    buildFeatures.viewBinding = true

    defaultConfig {
        applicationId = "org.mupen64plusae-mpn.v3.alpha"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            minifyEnabled false
            jniDebuggable true
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
		
        android.applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName = "Mupen64PlusAE-MPN.apk"
            }
        }
    }

    bundle {
        language {
            enableSplit false
        }
    }

    defaultConfig {
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    sourceSets {
        main {
            debug {
                jniLibs.srcDirs '../ndkLibs/libs/debug'
            }
            release {
                jniLibs.srcDirs '../ndkLibs/libs/release'
            }
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        resources {
            excludes += ['META-INF/AL2.0', 'META-INF/LGPL2.1', 'META-INF/beans.xml']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'org.mupen64plusae-mpn.v3.alpha'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.tvprovider:tvprovider:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    def lifecycle_version = "2.5.1"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    // Lifecycles only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"

    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    // alternately - if using Java8, use the following instead of lifecycle-compiler
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // optional - helpers for implementing LifecycleOwner in a Service
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    // optional - ProcessLifecycleOwner provides a lifecycle for the whole application process
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    // optional - ReactiveStreams support for LiveData
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycle_version"

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'org.tukaani:xz:1.9'

    implementation ('net.java.dev.jna:jna-platform:5.11.0') {
        exclude group: 'net.java.dev.jna', module: 'jna'
    }
    implementation 'net.java.dev.jna:jna:5.11.0@aar'
    testImplementation 'junit:junit:4.13.2'
    implementation project(':ae-bridge')
    implementation project(':miniupnp-bridge')
    implementation project(':mupen64plus-audio-android')
    implementation project(':mupen64plus-video-gliden64')
    implementation project(':mupen64plus-video-glide64mk2')
    implementation project(':mupen64plus-video-gln64')
    implementation project(':mupen64plus-rsp-parallel')
    implementation project(':mupen64plus-rsp-cxd4')
    implementation project(':mupen64plus-rsp-hle')
    implementation project(':mupen64plus-input-android')
    implementation project(':mupen64plus-input-raphnet')
    implementation project(':mupen64plus-video-rice')
    implementation project(':mupen64plus-video-angrylion-plus')
    implementation project(':mupen64plus-video-parallel')
    implementation project(':mupen64plus-core')
}

